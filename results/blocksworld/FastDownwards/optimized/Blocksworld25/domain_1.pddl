(define (domain multiagent-blocks)
  (:requirements :typing :negative-preconditions)
  (:types block)
  (:predicates
    (on ?x - block ?y - block)
    (ontable ?x - block)
    (clear ?x - block)
    (holding-vowel ?x - block)
    (holding-consonant ?x - block)
    (vowel-handempty)
    (consonant-handempty)
    (vowel ?x - block)
    (consonant ?x - block)
  )
  (:action vowel-pickup-from-table
    :parameters (?b - block)
    :precondition (and (ontable ?b) (clear ?b) (vowel-handempty) (vowel ?b))
    :effect (and (not (ontable ?b)) (not (clear ?b)) (holding-vowel ?b) (not (vowel-handempty)))
  )
  (:action vowel-pickup-from
    :parameters (?b - block ?x - block)
    :precondition (and (on ?b ?x) (clear ?b) (vowel-handempty) (vowel ?b))
    :effect (and (not (on ?b ?x)) (not (clear ?b)) (clear ?x) (holding-vowel ?b) (not (vowel-handempty)))
  )
  (:action vowel-putdown-on-table
    :parameters (?b - block)
    :precondition (and (holding-vowel ?b))
    :effect (and (ontable ?b) (clear ?b) (not (holding-vowel ?b)) (vowel-handempty))
  )
  (:action vowel-stack-on
    :parameters (?b - block ?x - block)
    :precondition (and (holding-vowel ?b) (clear ?x))
    :effect (and (on ?b ?x) (clear ?b) (not (clear ?x)) (not (holding-vowel ?b)) (vowel-handempty))
  )
  (:action consonant-pickup-from-table
    :parameters (?b - block)
    :precondition (and (ontable ?b) (clear ?b) (consonant-handempty) (consonant ?b))
    :effect (and (not (ontable ?b)) (not (clear ?b)) (holding-consonant ?b) (not (consonant-handempty)))
  )
  (:action consonant-pickup-from
    :parameters (?b - block ?x - block)
    :precondition (and (on ?b ?x) (clear ?b) (consonant-handempty) (consonant ?b))
    :effect (and (not (on ?b ?x)) (not (clear ?b)) (clear ?x) (holding-consonant ?b) (not (consonant-handempty)))
  )
  (:action consonant-putdown-on-table
    :parameters (?b - block)
    :precondition (and (holding-consonant ?b))
    :effect (and (ontable ?b) (clear ?b) (not (holding-consonant ?b)) (consonant-handempty))
  )
  (:action consonant-stack-on
    :parameters (?b - block ?x - block)
    :precondition (and (holding-consonant ?b) (clear ?x))
    :effect (and (on ?b ?x) (clear ?b) (not (clear ?x)) (not (holding-consonant ?b)) (consonant-handempty))
  )
)