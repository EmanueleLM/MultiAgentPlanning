(define (domain blocksworld)
    (:requirements :strips :typing :negative-preconditions)
    (:types block)
    (:predicates
        (on ?b1 - block ?b2 - block)
        (on-table ?b - block)
        (clear ?b - block)
        (hand-empty)
        (holding ?b - block)
    )

    (:action pick-up
        :parameters (?b - block)
        :precondition (and (on-table ?b) (clear ?b) (hand-empty))
        :effect (and (holding ?b)
                     (not (on-table ?b))
                     (not (clear ?b))
                     (not (hand-empty))
               )
    )

    (:action unstack
        :parameters (?b1 - block ?b2 - block)
        :precondition (and (on ?b1 ?b2) (clear ?b1) (hand-empty))
        :effect (and (holding ?b1)
                     (clear ?b2)
                     (not (on ?b1 ?b2))
                     (not (clear ?b1))
                     (not (hand-empty))
               )
    )

    (:action put-down
        :parameters (?b - block)
        :precondition (holding ?b)
        :effect (and (on-table ?b)
                     (clear ?b)
                     (hand-empty)
                     (not (holding ?b))
               )
    )

    (:action stack
        :parameters (?b1 - block ?b2 - block)
        :precondition (and (holding ?b1) (clear ?b2))
        :effect (and (on ?b1 ?b2)
                     (clear ?b1)
                     (hand-empty)
                     (not (holding ?b1))
                     (not (clear ?b2))
               )
    )
)