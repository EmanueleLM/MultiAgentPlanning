(define (domain floor-tile)
  (:requirements :typing :negative-preconditions :action-costs)
  (:types robot tile color stage - object)
  (:predicates
    (robot-at ?r - robot ?t - tile)
    (robot-has ?r - robot ?c - color)
    (available-color ?c - color)
    (clear ?t - tile)
    (unpainted ?t - tile)
    (painted ?t - tile ?c - color)
    (up ?from - tile ?to - tile)
    (down ?from - tile ?to - tile)
    (left ?from - tile ?to - tile)
    (right ?from - tile ?to - tile)
    (succ ?s1 - stage ?s2 - stage)
    (at-stage ?r - robot ?s - stage)
  )
  (:functions (total-cost))

  (:action change-color
    :parameters (?r - robot ?c - color ?c2 - color ?s - stage ?s2 - stage)
    :precondition (and
      (robot-has ?r ?c)
      (available-color ?c2)
      (not (robot-has ?r ?c2))
      (at-stage ?r ?s)
      (succ ?s ?s2)
    )
    :effect (and
      (not (robot-has ?r ?c))
      (robot-has ?r ?c2)
      (not (at-stage ?r ?s))
      (at-stage ?r ?s2)
      (increase (total-cost) 5)
    )
  )

  (:action paint-up
    :parameters (?r - robot ?x - tile ?y - tile ?c - color ?s - stage ?s2 - stage)
    :precondition (and
      (robot-at ?r ?x)
      (robot-has ?r ?c)
      (up ?x ?y)
      (clear ?y)
      (unpainted ?y)
      (at-stage ?r ?s)
      (succ ?s ?s2)
    )
    :effect (and
      (not (clear ?y))
      (painted ?y ?c)
      (not (unpainted ?y))
      (not (at-stage ?r ?s))
      (at-stage ?r ?s2)
      (increase (total-cost) 2)
    )
  )

  (:action paint-down
    :parameters (?r - robot ?x - tile ?y - tile ?c - color ?s - stage ?s2 - stage)
    :precondition (and
      (robot-at ?r ?x)
      (robot-has ?r ?c)
      (down ?x ?y)
      (clear ?y)
      (unpainted ?y)
      (at-stage ?r ?s)
      (succ ?s ?s2)
    )
    :effect (and
      (not (clear ?y))
      (painted ?y ?c)
      (not (unpainted ?y))
      (not (at-stage ?r ?s))
      (at-stage ?r ?s2)
      (increase (total-cost) 2)
    )
  )

  (:action up
    :parameters (?r - robot ?x - tile ?y - tile ?s - stage ?s2 - stage)
    :precondition (and
      (robot-at ?r ?x)
      (up ?x ?y)
      (clear ?y)
      (unpainted ?y)
      (at-stage ?r ?s)
      (succ ?s ?s2)
    )
    :effect (and
      (not (robot-at ?r ?x))
      (robot-at ?r ?y)
      (clear ?x)
      (not (clear ?y))
      (not (at-stage ?r ?s))
      (at-stage ?r ?s2)
      (increase (total-cost) 3)
    )
  )

  (:action down
    :parameters (?r - robot ?x - tile ?y - tile ?s - stage ?s2 - stage)
    :precondition (and
      (robot-at ?r ?x)
      (down ?x ?y)
      (clear ?y)
      (unpainted ?y)
      (at-stage ?r ?s)
      (succ ?s ?s2)
    )
    :effect (and
      (not (robot-at ?r ?x))
      (robot-at ?r ?y)
      (clear ?x)
      (not (clear ?y))
      (not (at-stage ?r ?s))
      (at-stage ?r ?s2)
      (increase (total-cost) 1)
    )
  )

  (:action right
    :parameters (?r - robot ?x - tile ?y - tile ?s - stage ?s2 - stage)
    :precondition (and
      (robot-at ?r ?x)
      (right ?x ?y)
      (clear ?y)
      (unpainted ?y)
      (at-stage ?r ?s)
      (succ ?s ?s2)
    )
    :effect (and
      (not (robot-at ?r ?x))
      (robot-at ?r ?y)
      (clear ?x)
      (not (clear ?y))
      (not (at-stage ?r ?s))
      (at-stage ?r ?s2)
      (increase (total-cost) 1)
    )
  )

  (:action left
    :parameters (?r - robot ?x - tile ?y - tile ?s - stage ?s2 - stage)
    :precondition (and
      (robot-at ?r ?x)
      (left ?x ?y)
      (clear ?y)
      (unpainted ?y)
      (at-stage ?r ?s)
      (succ ?s ?s2)
    )
    :effect (and
      (not (robot-at ?r ?x))
      (robot-at ?r ?y)
      (clear ?x)
      (not (clear ?y))
      (not (at-stage ?r ?s))
      (at-stage ?r ?s2)
      (increase (total-cost) 1)
    )
  )
)