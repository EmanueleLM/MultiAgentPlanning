(define (domain hanoi7)
  (:requirements :strips :typing :negative-preconditions)
  (:types disk peg stage)

  (:predicates
    (on ?d - disk ?s - disk)
    (on-peg ?d - disk ?p - peg)
    (top ?d - disk ?p - peg)
    (empty ?p - peg)
    (smaller ?d1 - disk ?d2 - disk)
    (current ?t - stage)
    (succ ?t1 - stage ?t2 - stage)
    (diff ?p1 - peg ?p2 - peg)
  )

  (:action move-from-peg-to-empty
    :parameters (?d - disk ?from - peg ?to - peg ?t - stage ?t2 - stage)
    :precondition (and
      (current ?t)
      (succ ?t ?t2)
      (top ?d ?from)
      (on-peg ?d ?from)
      (empty ?to)
      (diff ?from ?to)
    )
    :effect (and
      (not (current ?t))
      (current ?t2)
      (not (on-peg ?d ?from))
      (not (top ?d ?from))
      (empty ?from)
      (on-peg ?d ?to)
      (top ?d ?to)
      (not (empty ?to))
    )
  )

  (:action move-from-peg-to-disk
    :parameters (?d - disk ?from - peg ?to - peg ?topp - disk ?t - stage ?t2 - stage)
    :precondition (and
      (current ?t)
      (succ ?t ?t2)
      (top ?d ?from)
      (on-peg ?d ?from)
      (top ?topp ?to)
      (smaller ?d ?topp)
      (diff ?from ?to)
    )
    :effect (and
      (not (current ?t))
      (current ?t2)
      (not (on-peg ?d ?from))
      (not (top ?d ?from))
      (empty ?from)
      (on ?d ?topp)
      (top ?d ?to)
      (not (top ?topp ?to))
    )
  )

  (:action move-from-disk-to-empty
    :parameters (?d - disk ?s - disk ?from - peg ?to - peg ?t - stage ?t2 - stage)
    :precondition (and
      (current ?t)
      (succ ?t ?t2)
      (top ?d ?from)
      (on ?d ?s)
      (empty ?to)
      (diff ?from ?to)
    )
    :effect (and
      (not (current ?t))
      (current ?t2)
      (not (on ?d ?s))
      (not (top ?d ?from))
      (top ?s ?from)
      (on-peg ?d ?to)
      (top ?d ?to)
      (not (empty ?to))
    )
  )

  (:action move-from-disk-to-disk
    :parameters (?d - disk ?s - disk ?from - peg ?to - peg ?topp - disk ?t - stage ?t2 - stage)
    :precondition (and
      (current ?t)
      (succ ?t ?t2)
      (top ?d ?from)
      (on ?d ?s)
      (top ?topp ?to)
      (smaller ?d ?topp)
      (diff ?from ?to)
    )
    :effect (and
      (not (current ?t))
      (current ?t2)
      (not (on ?d ?s))
      (not (top ?d ?from))
      (top ?s ?from)
      (on ?d ?topp)
      (top ?d ?to)
      (not (top ?topp ?to))
    )
  )
)